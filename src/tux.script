#define module global[__FILE__]

#include "background.script"
#include "logo.script"
#include "input.script"
#include "message.script"
#include "lock.script"
#include "string.script"
#include "debug.script"

fun create_splash() {
    images.logo = Image("tux.png");
    images.background = Image("background.png");
    images.input = Image("input.png");
    images.spinner = Image("spinner.png");
    images.lock = Image("lock.png");

    screen_w = Window.GetWidth();
    screen_h = Window.GetHeight();
    screen_x = Window.GetX();
    screen_y = Window.GetY();
    center_x = screen_x + screen_w/2;
    center_y = screen_y + screen_h/2;

    create_background([center_x, center_y, 0], images);
    // Logo 40px above center
    logo = create_logo([center_x, center_y - images.logo.GetHeight()/2 - 40, 1], images);
    // Lock icon 10px below center
    lock = create_lock_icon([center_x, center_y + 10, 1], images);
    // Input 10px below lock icon
    input = create_input([center_x, center_y + 10 + lock.height + 10, 1], images);
    // Messages 10px below input
    message_box = create_message_box([center_x, input.y + input.height + 10, 1]);
    
    splash.logo = logo;
    splash.input = input;
    splash.lock = lock;
    splash.message_box = message_box;
    splash.progress = 0;
    return splash;
}

splash = create_splash();

fun onDisplayPassword(prompt, stars) {
    splash.input.show(prompt, stars);
    splash.logo.hide_spinner();
    splash.lock.show();
}

fun onDisplayNormal() {
    splash.input.hide();
    splash.logo.show_spinner();
}

fun onMessage(message) {
    splash.message_box.show_message(message);
    if (string_starts_with("cryptsetup:") && string_ends_with("set up successfully")) {
        splash.lock.hide();
    }
}

Plymouth.SetDisplayPasswordFunction(onDisplayPassword);
Plymouth.SetDisplayNormalFunction(onDisplayNormal);
Plymouth.SetRefreshFunction(splash.logo.update_spin);
Plymouth.SetMessageFunction(onMessage);
Plymouth.SetRootMountedFunction(onRootMounted);
