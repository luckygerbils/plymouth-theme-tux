#pragma once

module.log_font = "Fixed 10";
module.box_padding = 5;
module.num_log_columns = 100;
module.num_log_lines = 5;
module.log_message_sprites = [];

fun create_log(position) {
    sample_char = Image.Text("m", 1, 1, 1, 1, module.log_font);
    char_width = sample_char.GetWidth();
    char_height = sample_char.GetHeight();
    log_background_image = Image("log_background.png")
        .Scale(char_width * module.num_log_columns + module.box_padding*2, char_height * module.num_log_lines + module.box_padding * 2);
    
    module.log_sprite = Sprite(log_background_image);
    
    log_x = position[0] - log_background_image.GetWidth()/2;
    
    module.log_sprite.SetX(log_x);
    module.log_sprite.SetY(position[1]);
    module.log_sprite.SetZ(position[2]);
    
    for (i = 0; i < module.num_log_lines; i++) {
        module.log_message_sprites[i] = Sprite();
        module.log_message_sprites[i].SetX(log_x + module.box_padding);
        module.log_message_sprites[i].SetY(position[1] + module.box_padding + char_height * i);
        module.log_message_sprites[i].SetZ(position[2] + 1);
    }
    
    toggle_log();
    
    log.log_message = log_message;
    log.toggle = toggle_log;
    return log;
}

fun log_message(message) {
    new_message_image = Image.Text(message, 1, 1, 1, 1, module.log_font);
    
    for (i = 0; i < module.num_log_lines; i++) {
        if (module.log_message_sprites[i].GetImage() == NULL) {
            module.log_message_sprites[i].SetImage(new_message_image);
            return;
        }
    }
    
    shifted_message_image = new_message_image;
    for (i = module.num_log_lines - 1; i >= 0; i--) {
        tmp = module.log_message_sprites[i].GetImage();
        module.log_message_sprites[i].SetImage(shifted_message_image);
        shifted_message_image = tmp;
    }
}

fun toggle_log() {
    if (module.log_sprite.GetOpacity() == 1) {
        opacity = 0;
    } else {
        opacity = 1;
    }
    
    module.log_sprite.SetOpacity(opacity);
    for (i = 0; i < module.num_log_lines; i++) {
        module.log_message_sprites[i].SetOpacity(opacity);
    }
}
